#!/usr/bin/env dyalogscript
raw ← { ⊃ ⎕nget ('in/',⍵,'.in') 0 }
lines ← { ⊃ ⎕nget ('in/',⍵,'.in') 1 }

d01a ← { +⍥|.-⌿{⍵[⍋⍵]}¨↓⍉↑⍎¨⍵ } ⍝ lines '01'
d01b ← { {+⌿⍺+.×⍺∘.=⍵}⌿↓⍉↑⍎¨⍵ } ⍝ lines '01'

chk  ← ∘.(⍬≡~)∘(↓1 ¯1∘.×⍳3)-⌿¨⍨∘2
d02a ← { 1⊥,chk⍎¨⍵ }
d02b ← { 1⊥{1∊chk(⊂⍵),↓⍵⌿⍨⍤1∘.≠⍨⍳⍴⍵}∘⍎¨⍵ }

mrk ← 7 ⌊ ('mul(,)',⍕⎕d) (⍳×∊⍨) ⊢
rng ← { (¯1+⊣+⍳⍤-⍨)⌿(i⌿⍳≢⍵)[4 7∘.+⍸1 2 3 4 7 5 7 6⍷⍵⌿⍨i←1,2≠⌿⍵] }
flt ← { {⍵⌿⍨~∨⍀'don''t()'⍷⍵}¨⍵⊂⍨1@1⊢'do()'⍷⍵ }
d03a ← { +⌿×/↑⍎⍤⌷∘⍵∘⊂¨ rng mrk ⍵ }∘∊
d03b ← d03a flt∘∊

o ← { 1⊥∊⍺⍷(∊∘.⍺⍺⍨⍳≢⍵){⍵⍪⌽⍵}⌸⊢∊⍵ }
d04a ← 'XMAS'∘(+o+-o+⊢o+⊣o)
d04b ← { 1⊥∊∊∘2847 1599 1839 3087(⊃5⊥5 2⍴⊢)⌺3 3⊢'XMAS'⍳↑⍵ }
