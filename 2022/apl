#!/sbin/dyalogscript
indir ← './input'

I ← ⌷⍨∘⊂

FL ← { ⊃⎕NGET ⍵ 1 }

∇ in ← CB
	in ← ⎕SH 'xclip -sel clip -o'
∇

⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

day1 ← {
	in ← ⍎¨¨(⊂'')(≢¨⊆⊢) ⍵
	⎕←'--------- [day1]'
	⎕←p1 ← ⌈⌿+⌿¨in
	⎕←p2 ← (+⌿ ⊢ I 3↑⍒)+⌿¨in
}

day2 ← {
	⎕io ← 0
	in ← (⊂' ')(∊≠⊆⊢)¨ ⍵
	⎕←'--------- [day2]'
	ct ← (≢⍤⊢⌸ I ⍋∘∪) in
	op ← {,∘.(3|⍵+⍺⍺⍨)⍨⍳3}
	cb ← (1+⊣) + (3×⊢)
	⎕←p1 ← ct +.× (9⍴⍳3) cb (- op 4)
	⎕←p1 ← ct +.× (+ op 2) cb (9⍴⍳3)
}

day1 FL indir,'/1.in'
day2 FL indir,'/2.in'

⍝ vim: ft=apl
