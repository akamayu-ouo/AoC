d01 ← {
	D←'one' 'two' 'three' 'four' 'five' 'six' 'seven' 'eight' 'nine'
	d01a←{+⌿ (⍎⊃,⊃∘⌽)∘∊¨ (∊∘⎕D¨ ⊆¨ ⊢) ⍵}
	d01b←{+⌿10⊥⌷∘(0,,⍨⍳9)⊂⍉↑(⊃,⊃∘⌽)¨(⎕D,D)(0~⍨¨ ⍳∘≢⍤⊣ +.× ∘.⍷) ⍵}
	(d01a ⍵),(d01b ⍵)
}

d02 ← {
	G←↑{↑{⍵[⍋⍵;2]}¨{⍺,⌈⌿⍎¨⍵}⌸⌿↓⍉⌽{(2,⍨0.5×≢⍵)⍴⍵}2↓' '(≠⊆⊢)⍵~';,'}¨
	d02a ← +⌿∘⍸ ∧.≤∘14 13 12∘G
	d02b ← +⌿×/∘G
	(d02a ⍵),(d02b ⍵)
}

d03 ← {
	o ← 1 0 ¯1
	F ← {⍺{⍺∧⊃∨⌿o∘.⌽⊂⍵}⍣≡⍺∧⊃∨⌿,o∘.⊖o∘.⌽⊂⍵}
	V ← {⍎¨⊃,/⍺⊆⍨¨↓⍵}
	d03a←{
		d ← ⎕D∊⍨↑⍵
		+⌿ ⍵ V d F ~('.'∊⍨↑⍵)∨d
	}
	d03b←{
		n ← ≢⍉⍵
		s ← ('*'∘= ∧ 2 = (1⊥2</⎕D∊⍨∘,' ',⊢)⌺3 3)↑ ⍵
		+⌿(⊂⍵){×⌿↑a V (⎕D∊⍨↑a←⍺[o+⊃⍵]) F 1@(⊂2,⊃⌽⍵)⊢3 n⍴0}¨⍸s
	}
	(d03a ⍵),(d03b ⍵)
}

d04 ← {
	F ← {1⊥¨∊⍨/↑':|'∘(⍎¨1↓~⍤∊⍨⊆⊢)¨⍵}
	d04a ← 1⊥∘⌊.5×2*F
	d04b ← {
		m ← (⍳≢⍵)(↑ ⊂⍤⊣ ∊¨ +¨)⍳¨F⍵
		s ⊣ {s+←+/⍵⋄m+.×⍵}⍣≡⊢(≢⍵)⍴1 ⊣ s ← 0
	}
	(d04a ⍵),(d04b ⍵)
}

d07 ← {
	A ← (⊢,⍨5-+/)∘({⍵[1↓⍒⍵]}≢⍤⊢⌸)⍤1⍤~
	B ← ,∘'23456789TJQKA'
	F ← {(⍎¨V)+.×⍋⍋⍺(A,⍳⍨∘B)¨⍨⊃(H V)←↓⍉↑' '(≠⊆⊢)¨⍵}
	d07a ← ' '∘F
	d07b ← 'J'∘F
	(d07a ⍵),(d07b ⍵)
}

d09 ← {
	P ← ⍎¨' '∘(≠⊆⊢)
	F ← {¯1↓0(,⍨-,)⍣⍵⊢1}∘≢∘⍉
	d09a ← +⌿∘(⊢+.×F)∘↑ P¨
	d09b ← +⌿∘(⌽+.×F)∘↑ P¨
	(d09a ⍵),(d09b ⍵)
}

d11 ← {
	S ← {2÷⍨+⌿∊(|∘.-⍨)⍺⍺'#'=↑⍵}
	d11a ← (⍸{⍉(2-∨/⍵)⌿⍵}⍣2)S
	d11b ← {
		v ← +\0=+/'#'=↑⍵
		h ← +\0=+⌿'#'=↑⍵
		(({⍵+999999×v[⊃⍵],h[⊃⌽⍵]}¨⍸)S)⍵
	}
}
