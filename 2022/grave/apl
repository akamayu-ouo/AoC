day2 ← {
	⎕io ← 0
	in ← (⊂' ')(∊≠⊆⊢)¨ ⍵
	ct ← (≢⍤⊢⌸ I ⍋∘∪) in
	pt ← ⌽3|-∘.+⍨⍳3
	⎕←p1 ← ct +.× (,3×⊢pt) + (1+9⍴⍳3)
	⎕←p2 ← ct +.× (,1+⊖pt) + (3×9⍴⍳3)
}

day2 ← {
	⎕io ← 0
	in ← (⊂' ')(∊≠⊆⊢)¨ ⍵
	⎕←'--------- [day2]'
	ct ← (≢⍤⊢⌸ I ⍋∘∪) in
	op ← {,∘.(3|⍵+⍺⍺⍨)⍨⍳3}
	cb ← (1+⊣) + (3×⊢)
	⎕←p1 ← ct +.× (9⍴⍳3) cb (- op 4)
	⎕←p2 ← ct +.× (+ op 2) cb (9⍴⍳3)
}

day3 ← {
	in ← ⍵
	⍝ First attempt
	⎕←p1←+⌿{l←2÷⍨≢⍵ ⋄ ((⎕C,⊢)⎕A)⍳∪⍵[(⍳≢⍵)~⍳l]∩⍵[⍳l]}¨in
	⎕←p2←+⌿ {((⎕C,⊢)⎕A)⍳∪⍵}¨∩⌿⍤1⊢((3÷⍨⍴in),3)⍴in

	⎕←p1←+⌿(⎕A,⍨⎕C⎕A)⍳∊{⍵(∪↑∩↓)⍨2÷⍨≢⍵}¨in
}

day4 ← {
	⍝ First attempt
	in ← ↑⍎¨¨¨'-'∘(≠⊆⊢)¨¨','∘(≠⊆⊢)¨⍵
	⎕←p1←+⌿{a b←⍺⋄c d←⍵⋄((a≥c)∧(b≤d))∨((c≥a)∧(d≤b))}/in
	⎕←p2←+⌿{a b←⍺⋄c d←⍵⋄((b≥c)∧(a≤d))∨((c≥b)∧(d≤a))}/in
}

day5 ← {
	⍝ First attempt
	s m←(⊂'')∘(≢¨⊆⊢)⍵
	mv ← ⍎¨↑⎕D∘(∊⍨⊆⊢)¨m
	r←' '(⊂∘⌽≠⊢⍤/⊢)⍤1⊢1↓[2](⌽⍉↑s)⌿⍨⎕D∊⍨⊃⍤1⊢⌽⍉↑s
	⎕←p1←⊃¨⊃{(n f t)←⍺ ⋄ n ⌊← ≢f⊃⍵ ⋄ d←⍵ ⋄ (t⊃d),⍨←(⌽n↑f⊃⍵) ⋄ (f⊃d)↓⍨←n ⋄ d}⌿⌽(⊂r),⊂⍤1⊢mv
	⎕←p2←⊃¨⊃{(n f t)←⍺ ⋄ n ⌊← ≢f⊃⍵ ⋄ d←⍵ ⋄ (t⊃d),⍨←(n↑f⊃⍵) ⋄ (f⊃d)↓⍨←n ⋄ d}⌿⌽(⊂r),⊂⍤1⊢mv
}

day6 ← {
	⍝ First attempt
	in ← ⊃⍵
	⎕←p1←3+⊃⍸(∪=⍥≢⊢)¨4,⌿in
	⎕←p2←13+⊃⍸(∪=⍥≢⊢)¨14,⌿in
}

day6 ← {
	⍝ First attempt
	_EachRot ← { ⍉∘⌽⍤⊢⍀ ⍺⍺¨ ⌽∘⍉⍤⊢⍀ 4⌿⊂⍵ }
	⎕←p1←+⌿∊∨⌿{⍵>0,1↓[2]¯1⌽⌈\⍵}_EachRot 1+in
	⎕←p2←⌈⌿∊×/(↑⊂{(≢⍵)⍴((⍵-⍨≢⍺)@(=∘0)),⍉⍸⍤1⍉ <⍀ ⍵(⌷≤⍤1↓)⍺}¨⍳∘≢)_EachRot in
}

day11 ← { ⍝ Turtle
	⎕io ← 0
	sr ← {(⊣⌿⍺),⍵,⊢⌿⍺}
	l o d x y ← 1↓↓⍉('+*',⎕D)∘(∊⍨⊆⊢)¨↑(×∘≢¨⊆⊢)⍵
	o ← '×'⍨@('*'∘=)¨(∊'()'sr 3⌽⍤↑'⊢⊢'sr⊢)¨o
	A ← ⍉↑(⊂o),(d x y) ← ∊¨1↓(l d x y) ← ⍎¨¨¨(l d x y)
	_r ← {
		(a c q) ← ⍵
		0=≢a⊃q : (a+1) c q
		(o d x y) ← a⌷⍺
		b ← (x y) I 0≠d|v ← ⍺⍺ ⍎ o,⍕⊃(a⊃q)
		q[a] ↓⍨← c[a] +← 1
		q[b]  ,← v
		a c q
	}
	_g ← {⍺(⍺⍺_r)⍣{(0⊃⍺)=(≢1⊃⍵)}⊢0⍨@0⊢⍵}
	_h ← {⊃2×⌿(⊂∘⍒⌷⊢) 1⊃ ⍺ (⍺⍺_g⍣⍵⍵) (0 (0⍨¨⍵) ⍵)}
	p1←A    (⌊÷∘3)_h 00020 ⊢ l
	p2←A ((×⌿d)∘|)_h 10000 ⊢ l
	p1 p2
}

day11 ← { ⍝ Kind of slow
	⎕io ← 0
	sr ← {(⊣⌿⍺),⍵,⊢⌿⍺}
	oo ← '×'⍨@('*'∘=)(∊'()'sr 3⌽⍤↑'⊢⊢'sr⊢)
	fmt ← 1↓(oo¨@2)∘(⍎¨¨@1 3 4 5)
	xy ← ⊃,¨⌿¯2↑(d x y) ← ∊¨¯3↑(v t d x y) ← ⊂[1]∊¨fmt⍉('+*',⎕D)∘(∊⍨⊆⊢)¨↑(×∘≢¨⊆⊢)⍵
	t ← t,¨(⍕¨⍳≢v),¨⊂'⊃v'
	_r ← {
		c +← ≢¨⊃⌽(c q v) ← ⍵
		to ← xy I¨ 0≠d | nv ← ⍺⍺ ⍎¨t
		bk ← ~fw ← ∊ to > ⍳≢v
		to ← ∊to
		nv ← ∊nv
		q[bk⌿to] ,← bk⌿nv
		v[fw⌿to] ,← fw⌿nv ⊣ v ← ⍬⍨¨ v
		c q v
	}
	_g ← {((⍺⍺_r)⍣{0=≢∊⊃⌽⍺}) ⍵ I 0 2 1}
	_h_ ← { ⊃ 2×⌿ (⊂∘⍒⌷⊢)⊃ (⍺⍺_g⍣⍵⍵) ((⊂0⍨¨),⊂,(⊂⍬⍨¨))⍵}
	p1←  (⌊⍤÷∘3) _h_ 20    ⊢ v
	p2← (×⌿d)∘|_h_ 10000 ⊢ v
	p1 p2
}

day12 ← {
	⍝ First attempt
	fx ← 'a'⍨@(=∘'S')⊢'z'⍨@(=∘'E')⊢
	st ← ⊂⍤⊢⌺3 3
	p1 ← {h ← ⎕UCS fx ⍵ ⋄ s e ← 'S' 'E'=¨(⊂⍵) ⋄ f ← (1≥-∘st⍨)h ⋄ n ← 0 ⋄ n ⊣ {n+←1⋄ (1∊,I(2×⍳4)⍨)¨f∧st ⍵ }⍣{1∊⍺∧e} ⊢ s } ↑⍵
	p2 ← {h ← ⎕UCS fx ⍵ ⋄ a e ← 'a' 'E'=¨(⊂⍵) ⋄ f ← (1≤-⍨∘st⍨)h ⋄ n ← 0 ⋄ n ⊣ {n+←1⋄ (1∊,I(2×⍳4)⍨)¨f∧st ⍵ }⍣{1∊⍺∧a} ⊢ e } ↑⍵
}

day13 ← {
	p1 ← +/⍸</⍋⍤1⊢⍎¨↑(×∘≢¨⊆⊢)∊¨('(⊂⍬,'⍨¨@('['∘=))¨∊¨(',⍬)'⍨¨@(']'∘=))¨⍵
	ee←(⊂∘⍋⌷⊢)((⊂(⊂2)) (⊂(⊂6))),⊃⍎¨¨,/(×∘≢¨⊆⊢)∊¨('(⊂⍬,'⍨¨@('['∘=))¨∊¨(',⍬)'⍨¨@(']'∘=))¨⍵
	p2 ← ×⌿(⊂ee) ⍳¨ ((⊂(⊂2)) (⊂(⊂6)))
}

⍝ vim: ft=apl
