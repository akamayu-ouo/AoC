#!/sbin/dyalogscript
indir ← './input'

I ← ⌷⍨∘⊂

FL ← { ⊃⎕NGET ⍵ 1 }

∇ in ← CB
	in ← ⎕SH 'xclip -sel clip -o'
∇

⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

day1 ← {
	⎕←'--------- [day1]'
	in ← ⍎¨¨(⊂'')(≢¨⊆⊢) ⍵
	⎕←p1 ← ⌈⌿+⌿¨in
	⎕←p2 ← (+⌿ ⊢ I 3↑⍒)+⌿¨in
}

day2 ← {
	⎕←'--------- [day2]'
	⎕io ← 0
	in ← (⊂' ')(∊≠⊆⊢)¨ ⍵
	m  ← ↑(⊂'ABC' 'XYZ')⍳¨¨in
	⎕←p1←+⌿(1+1⌷⍉m),(3×3|4+-⍨/m)
	⎕←p2←+⌿(3×1⌷⍉m),(1+3|2++⍨/m)
}

day3 ← {
	⎕←'--------- [day3]'
	⎕io ← 1
	in ← ⍵
	s  ← +⌿(⎕A,⍨⎕C⎕A) ⍳∘∊ ∪⍤∩⌿¨
	⎕←p1←s (⊢(↑,⍥⊂↓)⍨2÷⍨≢)¨in
	⎕←p2←s in⊂⍨1 0 0⍴⍨≢in
}

day4 ← {
	⎕←'--------- [day4]'
	(s1 e1 s2 e2)←⊂[1]↑⎕d∘(⍎¨∊⍨⊆⊢)¨⍵
	⎕←p1←+⌿((s1≥s2)∧(e1≤e2))∨((s2≥s1)∧(e2≤e1))
	⎕←p2←+⌿((e1≥s2)∧(s1≤e2))∨((s2≥e1)∧(e2≤s1))
}

day5 ← {
	⎕←'--------- [day5]'
	s m ← (×∘≢¨⊆⊢)⍵
	mv ← ⎕D∘(⍎¨∊⍨⊆⊢)¨m
	r ← ' '(⊂¯1↓≠⊢⍤⌿⊢)⍤1⊢(⍸¨' '≠⊢⌿s)⌷⍉↑s
	l ← ⌽mv,⍨⊂r
	_p ← {(n f t)←⍺ ⋄ (f⊃h)↓⍨←≢(t⊃h),⍨←⍺⍺n↑f⊃h←⍵ ⋄ h}
	⎕←p1←⊃¨↑ ⌽_p⌿ l
	⎕←p2←⊃¨↑ ⊢_p⌿ l
}

day6 ← {
	⎕←'--------- [day6]'
	⎕io ← 0
	in ← ⊃⍵
	f ← ⊣ + (≢¨∪⌿) ⍳ ⊣
	⎕←p1← 4 f in
	⎕←p2←14 f in
}

day7 ← {} ⍝ WIP

day8 ← {
	⎕←'--------- [day8]'
	in ← ⍎¨↑⍵
	_erot_ ← { ⊃⍺⍺⌿ ⍉∘⌽⍤⊢⍀ ⍵⍵¨ ⌽∘⍉⍤⊢⍀ 4⌿⊂⍵ }
	⎕←p1←+⌿∊ ∨_erot_{⍵>(⍴⍵)⍴0⍪⌈⍀⍵} 1+in
	⎕←p2←⌈⌿∊ ×_erot_(↑⊂{(⍵-⍨≢⍺)@(=∘0), ⍉⍸⍤1⍉ <⍀ ⍵(⌷≤⍤1↓)⍺}¨⍳∘≢) in
}

day1 FL indir,'/1.in'
day2 FL indir,'/2.in'
day3 FL indir,'/3.in'
day4 FL indir,'/4.in'
day5 FL indir,'/5.in'
day6 FL indir,'/6.in'
day8 FL indir,'/8.in'

⍝ vim: ft=apl
