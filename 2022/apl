#!/sbin/dyalogscript
indir ← './input'

I ← ⌷⍨∘⊂

FL ← { ⊃⎕NGET ⍵ 1 }

∇ in ← CB
	in ← ⎕SH 'xclip -sel clip -o'
∇

⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝

day1 ← {
	in ← ⍎¨¨(⊂'')(≢¨⊆⊢) ⍵
	⎕←'--------- [day1]'
	⎕←p1 ← ⌈⌿+⌿¨in
	⎕←p2 ← (+⌿ ⊢ I 3↑⍒)+⌿¨in
}

day2 ← {
	⎕io ← 0
	in ← (⊂' ')(∊≠⊆⊢)¨ ⍵
	⎕←'--------- [day2]'
	m  ← ↑(⊂'ABC' 'XYZ')⍳¨¨in
	⎕←p1←+⌿(1+1⌷⍉m),(3×3|4+-⍨/m)
	⎕←p2←+⌿(3×1⌷⍉m),(1+3|2++⍨/m)
}

day3 ← {
	⎕io ← 1
	in ← ⍵
	⎕←'--------- [day3]'
	s  ← +⌿(⎕A,⍨⎕C⎕A) ⍳∘∊ ∪⍤∩⌿¨
	⎕←p1←s (⊢(↑,⍥⊂↓)⍨2÷⍨≢)¨in
	⎕←p2←s in⊂⍨1 0 0⍴⍨≢in
}

day1 FL indir,'/1.in'
day2 FL indir,'/2.in'
day3 FL indir,'/3.in'

⍝ vim: ft=apl
